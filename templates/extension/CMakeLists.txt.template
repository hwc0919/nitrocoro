cmake_minimum_required(VERSION 3.15)
project(nitrocoro-{{NAME}})

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(NITROCORO_BUILD_DIR "" CACHE PATH "NitroCoro build directory (preferred)")
set(NITROCORO_SOURCE_DIR "" CACHE PATH "NitroCoro source directory (fallback)")

if(NITROCORO_BUILD_DIR)
    if(NOT NITROCORO_SOURCE_DIR)
        if(EXISTS "${NITROCORO_BUILD_DIR}/CMakeCache.txt")
            file(STRINGS "${NITROCORO_BUILD_DIR}/CMakeCache.txt" _source_dir_line 
                 REGEX "^CMAKE_HOME_DIRECTORY:INTERNAL=")
            if(_source_dir_line)
                string(REGEX REPLACE "^CMAKE_HOME_DIRECTORY:INTERNAL=" "" NITROCORO_SOURCE_DIR "${_source_dir_line}")
                message(STATUS "Deduced NITROCORO_SOURCE_DIR from CMakeCache.txt: ${NITROCORO_SOURCE_DIR}")
            endif()
        endif()

        if(NOT NITROCORO_SOURCE_DIR)
            get_filename_component(NITROCORO_SOURCE_DIR "${NITROCORO_BUILD_DIR}/.." ABSOLUTE)
            message(STATUS "Deduced NITROCORO_SOURCE_DIR from parent directory: ${NITROCORO_SOURCE_DIR}")
        endif()
    endif()

    find_library(NITROCORO_LIB nitrocoro PATHS ${NITROCORO_BUILD_DIR} NO_DEFAULT_PATH REQUIRED)
    add_library(nitrocoro STATIC IMPORTED)
    set_target_properties(nitrocoro PROPERTIES
        IMPORTED_LOCATION ${NITROCORO_LIB}
        INTERFACE_INCLUDE_DIRECTORIES "${NITROCORO_SOURCE_DIR}/include"
    )
elseif(NITROCORO_SOURCE_DIR)
    add_subdirectory(${NITROCORO_SOURCE_DIR} ${CMAKE_BINARY_DIR}/nitrocoro)
else()
    find_package(nitrocoro QUIET)
    if(NOT nitrocoro_FOUND)
        message(FATAL_ERROR "NitroCoro not found. Please set NITROCORO_BUILD_DIR, NITROCORO_SOURCE_DIR, or install nitrocoro")
    endif()
endif()

add_library(nitrocoro-{{NAME}} STATIC
    src/{{CLASS}}.cc
)

target_include_directories(nitrocoro-{{NAME}} PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}/include
)

target_link_libraries(nitrocoro-{{NAME}} PUBLIC nitrocoro)

add_executable({{NAME}}_test tests/{{NAME}}_test.cc)
target_link_libraries({{NAME}}_test PRIVATE nitrocoro-{{NAME}})
add_test(NAME {{NAME}}_test COMMAND {{NAME}}_test)
